import com.android.build.OutputFile
import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
//apply from: 'gradle-mvn-push.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def getCurrentFlavor() {
    String task = getGradle().getStartParameter().getTaskRequests().toString()
    Matcher matcher = Pattern.compile("(assemble|generate)\\w*(Release|Debug)").matcher(task)
    if (matcher.find()) return matcher.group(2).toLowerCase() else {
        println "Warning: No match found for $task"
        return "debug"
    }
}

android {
    buildToolsVersion '27.0.3'
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.appgo.appgopro"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode 17
        versionName "2.2.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "zh-rCN"

        manifestPlaceholders = [
                JPUSH_PKGNAME: "com.appgo.appgopro",
                JPUSH_APPKEY : "c7fb4bd891952f3081bdf1d4", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }
    buildTypes {
        debug {
            multiDexEnabled true
            pseudoLocalesEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions.checkReleaseBuilds false
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86'
            universalApk true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api project(':plugin')
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:gridlayout-v7:$supportLibraryVersion"
    implementation 'com.futuremind.recyclerfastscroll:fastscroll:0.2.5'
    implementation 'com.evernote:android-job:1.2.4'
    implementation "com.google.android.gms:play-services-vision:$playServicesVersion"
    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"
    implementation "com.google.firebase:firebase-config:$playServicesVersion"
    implementation 'com.j256.ormlite:ormlite-android:5.1'
    implementation 'com.mikepenz:materialdrawer:6.0.6'
    implementation 'com.google.code.gson:gson:2.7'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation "com.takisoft.fix:preference-v7-simplemenu:$takisoftFixVersion"
    implementation 'com.twofortyfouram:android-plugin-api-for-locale:1.0.2'
    implementation 'dnsjava:dnsjava:2.1.8'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'
    implementation 'net.glxn.qrgen:android:2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation 'xyz.belvi.mobilevision:barcodescanner:2.0.3'
    implementation 'commons-io:commons-io:2.4'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0@aar'
    implementation 'com.wang.avi:library:2.1.3@aar'
    implementation 'me.dm7.barcodescanner:core:1.9.8@aar'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8@aar'
    implementation 'cn.jiguang.sdk:jcore:1.1.9@aar'
    implementation 'cn.jiguang.sdk:jpush:3.1.1@aar'
    testImplementation "junit:junit:$junitVersion"
    //    androidTestImplementation "com.android.support.test:runner:$androidTestVersion"
    //    androidTestImplementation "com.android.support.test.espresso:espresso-core:$androidEspressoVersion"
    implementation files('libs/alipaySdk-20180403.jar')
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true;
    }
}
repositories {
    mavenCentral()
    flatDir {
        dirs 'aars'
    }
}

apply plugin: 'com.google.gms.google-services'

ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86: 3]
if (getCurrentFlavor() == 'release') android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def offset = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (offset != null) output.versionCodeOverride = variant.versionCode + offset
    }
}

